$$include '../../meta/macros.ptl'

glyph-module

glyph-block LetterLike-Fraktur-Lower-AQ : begin
	glyph-block-import Common-Derivatives
	glyph-block-import CommonShapes
	glyph-block-import LetterLike-Fraktur-Common : LowerDf S F T fraktur-stroke change-pen
	glyph-block-import LetterLike-Fraktur-Common : DecoSizeX DecoSizeY SlopeA SlopeB
	glyph-block-import LetterLike-Fraktur-Common : Wave PHexTop PHexBot RBDecoration

	define [LeftHalfShape mode box] : fraktur-stroke S
		corner box.right [post@slope SlopeB]
		corner [box.xp PHexTop] box.top
		g2.down.mid (box.left - OX) [pre@mix@post 0.5]
		corner [box.xp PHexBot] box.bot
		corner box.right [pre@slope SlopeA]

	create-glyph "frak/a" 0x1D51E : glyph-proc
		local df : include : LowerDf 1
		include : df.markSet.e

		local aBox : S.box XH 0 df.leftSB df.rightSB

		local leftStroke : include : LeftHalfShape 'a' aBox
		local rightStroke : include : fraktur-stroke S
			flat [leftStroke.first].x [leftStroke.first].y
			RBDecoration [leftStroke.last].x aBox.bot [leftStroke.last].y

	create-glyph "frak/g" 0x1D524 : glyph-proc
		local df : include : LowerDf 1
		include : df.markSet.p

		local aBox : S.box XH 0 df.leftSB df.rightSB
		local gBox : S.box XH Descender df.leftSB df.rightSB

		local leftStroke : include : LeftHalfShape 'p' aBox
		local rightStroke : include : fraktur-stroke S
			corner [leftStroke.first].x [leftStroke.first].y
			corner pre@ [leftStroke.last].y
			g2 (pre@ <+> 0.5 * DecoSizeX) (post@ <+> SmallArchDepthA)
			hookend (sw -- S.thick) gBox.bot
			g4 gBox.left (pre@ <+> SHook)


	create-glyph "frak/q" 0x1D52E : glyph-proc
		local df : include : LowerDf 1
		include : df.markSet.p

		local aBox : S.box XH 0 df.leftSB df.rightSB
		local qBox : S.box XH Descender df.leftSB df.rightSB

		local leftStroke : include : LeftHalfShape 'p' aBox
		local rightStroke : include : fraktur-stroke S
			flat [leftStroke.first].x [leftStroke.first].y
			RBDecoration [leftStroke.last].x qBox.bot [leftStroke.last].y
