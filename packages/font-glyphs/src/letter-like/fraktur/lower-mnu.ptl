$$include '../../meta/macros.ptl'

import [mix] from "@iosevka/util"

glyph-module

glyph-block LetterLike-Fraktur-Lower-MNU : begin
	glyph-block-import Common-Derivatives
	glyph-block-import CommonShapes
	glyph-block-import Letter-Shared : CreateAccentedComposition
	glyph-block-import Mark-Shared-Metrics : markMiddle markDotsRadius
	glyph-block-import Mark-Above : StdAnchors aboveMarkTop aboveMarkBot aboveMarkMid aboveMarkStack
	glyph-block-import LetterLike-Fraktur-Common : LowerDf S M fraktur-stroke change-pen
	glyph-block-import LetterLike-Fraktur-Common : DecoSizeX DecoSizeY SlopeA SlopeB
	glyph-block-import LetterLike-Fraktur-Common : Wave PHexTop PHexBot LTDecoration RBDecoration

	create-glyph "frak/m" 0x1D52A : glyph-proc
		local df : include : LowerDf para.diversityM 3
		include : df.markSet.e

		local box : M.box XH 0 df.leftSB df.rightSB
		include : fraktur-stroke M
			LTDecoration box.left box.top
			corner box.left box.bot
		include : fraktur-stroke M
			corner box.left [post@slope SlopeA]
			corner [box.xp : mix 0 0.5 : mix 0.5 1 PHexTop] box.top
			corner box.xMid [pre@slope SlopeB]
			corner box.xMid box.bot
		include : fraktur-stroke M
			corner box.xMid [post@slope SlopeA]
			corner [box.xp : mix 0.5 1 : mix 0.5 1 PHexTop] box.top
			corner box.right [pre@slope SlopeB]
			RBDecoration pre@ box.bot

	create-glyph "frak/n" 0x1D52B : glyph-proc
		local df : include : LowerDf 1
		include : df.markSet.e

		local box : S.box XH 0 df.leftSB df.rightSB
		include : fraktur-stroke S
			LTDecoration box.left box.top
			corner box.left box.bot
		include : fraktur-stroke S
			corner box.left [post@slope SlopeA]
			corner [box.xp : mix 0.5 1 PHexTop] box.top
			corner box.right [pre@slope SlopeB]
			RBDecoration pre@ box.bot

	create-glyph "frak/u" 0x1D532 : glyph-proc
		local df : include : LowerDf 1
		include : df.markSet.e

		local box : S.box XH 0 df.leftSB df.rightSB
		local leftStroke : include : fraktur-stroke S
			LTDecoration box.left box.top
			corner pre@ [post@slope SlopeB]
			corner [box.xp PHexBot] box.bot
			corner box.right [pre@slope SlopeA]
		include : fraktur-stroke S
			LTDecoration box.right box.top
			RBDecoration box.right box.bot [leftStroke.last].y
